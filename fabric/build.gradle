plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        name = "TomTheGeek-repo"
        url = "https://maven.tomthegeek.ml/releases/"
    }
}

configurations {
    shadowCommon
    compileClasspath.extendsFrom shadowCommon
    runtimeClasspath.extendsFrom shadowCommon
}

architectury {
    platformSetupLoomIde()
    fabric()
}
def ENV = System.getenv();

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

    //modRuntime 'com.github.CaffeineMC:Lithium-fabric:1.17.x~stable-SNAPSHOT'
    //modRuntime "com.github.astei:lazydfu:0.1.2"

    // Testing with /disguise command
    //modRuntime 'com.github.samolego:MobDisguises:d6cc302588'

    //modRuntime "com.github.samolego.Taterzens:taterzens-fabric:1.4.0"
    //modRuntime files('libs/mobdisguises-1.0.3-fabric.jar')

    // Taterzens
    //modRuntime 'com.github.samolego.Taterzens:taterzens-fabric:1.3.0'

    developmentFabric(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        if (ENV.MAVEN_URL) {
            maven {
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
                url ENV.MAVEN_URL
            }
        } else {
            mavenLocal()
        }
    }
}
